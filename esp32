#include <esp_now.h>
#include <WiFi.h>
#include <HX711.h>

// --- HX711 ---
#define DT 33 
#define SCK 32 

// --- Configuração do ESP-NOW ---
uint8_t broadcastAddress[] = {0x34, 0x5F, 0x45, 0xE7, 0x6D, 0x44}; 

// Estrutura para os dados a serem enviados
typedef struct message {
  unsigned long timestamp;
  float entrada_mv;
  char classificacao[20]; // Alterado para um array de caracteres
} message;

message dataSonda;
esp_now_peer_info_t peerInfo;
HX711 scale;

// --- Variáveis ---
float entrada_mv = 0;
String classificacao_str = ""; // Nome da variável alterado para evitar conflito

// Função para monitorar o status do envio
void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status) {
  Serial.print("\r\nStatus de Envio: ");
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "Sucesso" : "Falha");
}

void setup() {
  Serial.begin(115200);
  WiFi.mode(WIFI_STA);

  if (esp_now_init() != ESP_OK) {
    Serial.println("Erro ao inicializar ESP-NOW");
    return;
  }
  esp_now_register_send_cb(OnDataSent);

  memcpy(peerInfo.peer_addr, broadcastAddress, 6);
  peerInfo.channel = 0;
  peerInfo.encrypt = false;
  if (esp_now_add_peer(&peerInfo) != ESP_OK){
    Serial.println("Falha ao adicionar peer");
    return;
  }

  scale.begin(DT, SCK);
  scale.set_gain(128);

  Serial.println("Sistema ESP-NOW para Sonda iniciado.");
  delay(500);
}

void loop() {
  if (scale.is_ready()) {
    long leitura = scale.read();
    entrada_mv = (leitura / 8388607.0) * 1200.0 * 0.0123;

    if (entrada_mv > 5.7) {
      classificacao_str = "Rica";
    } else if (entrada_mv >= 3.5) {
      classificacao_str = "Ideal";
    } else {
      classificacao_str = "Pobre";
    }

    dataSonda.timestamp = millis();
    dataSonda.entrada_mv = entrada_mv;
    
    // Copia a String para o array de caracteres na struct
    classificacao_str.toCharArray(dataSonda.classificacao, sizeof(dataSonda.classificacao));
    
    if (entrada_mv > 0.9){
    esp_err_t result = esp_now_send(broadcastAddress, (uint8_t *) &dataSonda, sizeof(dataSonda));
    

      if (result == ESP_OK) {
       Serial.println("Dados enviados com sucesso!");
      } else {
       Serial.println("Erro ao enviar os dados!");
     }
    }

    if (entrada_mv > 0.9) {
      Serial.print("Tensao (mV): ");
      Serial.print(entrada_mv, 2);
      Serial.print(" | Mistura: ");
      Serial.println(classificacao_str);
    }
  }
  delay(300);
}
